model Notenboom2023_DON_Human()

#Created: Carolina Vogs, September 2025
#Checked:

#===========================================================
# DON PBK (Notenboom 2023):
# GI lumen --kabs--> Liver --Cl_met--> Metabolites
# Blood <-> Liver, Blood <-> Rest (perfusion-limited)
# https://www.ncbi.nlm.nih.gov/pubmed/37755995
#===========================================================

#------------------------
# Body size & base volumes
#------------------------
BW   = 70                       # kg
VLc  = 0.026                    # L/kg (liver)
VBc  = 0.079                    # L/kg (blood)

VL   = VLc*BW                   # L
VB   = VBc*BW                   # L

VLum = 1.29                     # Lumen volume L 

VRest := 0.84*BW - VL - VB  # Lumped Rest-of-body volume L 

#------------------------
# Blood Flows
#------------------------
QC    = 15*BW^0.74            # L/h (cardiac output)
QLc   = 0.227
QL    = QLc*QC                # L/h (liver)
QRest = (1 - QLc)*QC          # L/h (rest)

#------------------------
# Absorption & hepatic metabolism
#------------------------
kabs        = 22                 # 1/h  (effective GI→liver absorption)

# Metabolic clearance (L/h/kg → scaled to BW)
CLintMet1  = 0.07*BW           # L/h  (DON → DON-3-GlcA)
CLintMet2 = 0.32*BW           # L/h  (DON → DON-15-GlcA)

#------------------------
# Partition coefficients
#------------------------
PL = 6.56
PRest = 1

#------------------------
# Compartments (sizes)
#------------------------
compartment Lumen;   Lumen = VLum
compartment Liver;   Liver = VL
compartment Blood;   Blood = VB
compartment Rest;    Rest  = VRest
 

#------------------------
# Species (amount states)
#------------------------
substanceOnly species ALumen in Lumen
substanceOnly species ALiver in Liver
substanceOnly species ABlood in Blood
substanceOnly species ARest  in Rest
substanceOnly species ALiverMet1  in Liver
substanceOnly species ALiverMet2  in Liver


#------------------------
# Initial conditions
#------------------------
ALumen = 0; 
ALiver = 0; 
ABlood = 0; 
ARest = 0; 
ALiverMet1 = 0; 
ALiverMet2 = 0; 

#------------------------
# Concentrations (µmol/L)
#------------------------
CLumen := ALumen / Lumen
CLiver := ALiver / Liver
CBlood := ABlood / Blood
CRest  := ARest  / Rest

#------------------------
# Reactions / Transfers
#------------------------

# GI absorption (first-order amount → liver)
ALumen -> ALiver;     kabs * ALumen

# Perfusion-limited exchanges (blood ↔ liver & rest)
ABlood   -> ALiver;     QL * CBlood
ALiver   -> ABlood;     QL * CLiver/PL

ABlood   -> ARest;    QRest * CBlood
ARest  	-> ABlood;     QRest * CRest/PRest

# Hepatic metabolism 
ALiver   -> ALiverMet1;     CLintMet1  * CLiver
ALiver   -> ALiverMet2;    CLintMet2 * CLiver

end
