model Husoy2024_PFOA_Human()

#Created: Carolina Vogs, September 2025
#Checked:

#===========================================================
# PFOA PBK (Husoy et al. 2024):
# https://www.ncbi.nlm.nih.gov/pubmed/37839534
#===========================================================

#------------------------
# Body size & base volumes
#------------------------
BW   = 70                       # kg
VLc  = 0.026                    # L/kg (liver)
Vplasmac  = 0.0428              # L/kg (plasma)
VGIc  = 0.017			# L/kg (gut)

VL   = VLc*BW                   # L
Vplasma   = Vplasmac*BW         # L
VGI   = VGIc*BW                   # L

VRest := 0.84*BW - VL - Vplasma - VGI  # Lumped Rest-of-body volume L 

#------------------------
# Plasma Flows
#------------------------
QC    = 12.5*BW^0.75            # L/h (cardiac output)
QPlasma   = QC*(1-Htc)		# L/h cardiac output adjusted for plasma flow
QLc   = 0.23
QGIc   = 0.181

QL    = QLc*QPlasma                # L/h (liver)
QGI    = QGIc*QPlasma                # L/h (liver)
QRest = QPlasma - QL - QGI          # L/h (rest)

Htc = 0.44
Free = 0.02

kfeC = 0.00000217		# L/h/kg
kfe = kfeC * BW			# L/h

kbiliaryC = 0.000109		# L/h/kg
kbiliary = kbiliaryC * BW	# L/h

#------------------------
# Partition coefficients
#------------------------
PL = 2.2
PG = 0.05
PRest = 1

#------------------------
# Compartments (sizes)
#------------------------
compartment GI;      GI = VGI
compartment Liver;   Liver = VL
compartment Plasma;  Plasma = Vplasma
compartment Rest;    Rest  = VRest
compartment Feces;   Feces  = 1


#------------------------
# Species (amount states)
#------------------------
substanceOnly species AGI in GI
substanceOnly species ALiver in Liver
substanceOnly species APlasma in Plasma
substanceOnly species ARest  in Rest
substanceOnly species AFeces  in Feces


#------------------------
# Initial conditions
#------------------------
AGI = 0 
ALiver = 0 
APlasma = 0 		# free fraction in plasma
ARest = 0 
AFeces = 0


#------------------------
# Concentrations 
#------------------------
CGI := AGI / GI
CLiver := ALiver / Liver
CPlasma := APlasma / Plasma		# free fraction in plasma
CPlasmaTotal := CPlasma/Free		# Total PFOS concentration in plasma
CRest  := ARest  / Rest

#------------------------
# Reactions / Transfers
#------------------------

# GI ↔ liver & Feces
AGI -> ALiver;     QGI * Free * CGI/PG
ALiver -> AGI; 	   kbiliary * Free * CLiver/PL
AGI -> AFeces;     kfe * Free * CGI/PG


# Perfusion-limited exchanges (Plasma ↔ gut & liver & rest)
APlasma   -> AGI;     QGI * Free * CPlasma

APlasma   -> ALiver;     QL * Free * CPlasma
ALiver   -> APlasma;     (QL+QGI) * Free * CLiver/PL

APlasma   -> ARest;    QRest * Free * CPlasma
ARest  	-> APlasma;     QRest * Free * CRest/PRest


end
