model Deepika_PFOS_Human()

#Created: Carolina Vogs, September 2025
#Checked:

#===========================================================
# PFOS PBK (Deepika et al. 2021):
# used values from Loccisano PFOA for now
# equations are built up as represented in the schema, as code is unavailable
# https://www.ncbi.nlm.nih.gov/pubmed/34000270
#===========================================================

#------------------------
# Body size & base volumes
#------------------------
BW   = 70                       # kg
VLc  = 0.026                    # L/kg (liver)
VPlasmac  = 0.0428              # L/kg (plasma)
VGc  = 0.0171			# L/kg (gut)

VL   = VLc*BW                   # L
VPlasma   = VPlasmac*BW                   # L
VG   = VGc*BW                   # L

VRest := 0.84*BW - VL - VPlasma - VG  # Lumped Rest-of-body volume L 

#------------------------
# Plasma Flows
#------------------------
QC    = 12.5*BW^0.75            # L/h (cardiac output)
QPlasma   = QC*(1-Htc)		# L/h cardiac output adjusted for plasma flow
QLc   = 0.25
QGc   = 0.181

QL    = QLc*QPlasma                # L/h (liver)
QG    = QGc*QPlasma                # L/h (liver)
QRest = QPlasma - QL - QG          # L/h (rest)

Htc = 0.44
Free = 0.02

#------------------------
# Partition coefficients
#------------------------
PL = 2.56
PG = 0.05
PRest = 1

#------------------------
# Compartments (sizes)
#------------------------
compartment GI;      GI = VG
compartment Liver;   Liver = VL
compartment Plasma;  Plasma = VPlasma
compartment Rest;    Rest  = VRest


#------------------------
# Species (amount states)
#------------------------
substanceOnly species AGI in GI
substanceOnly species ALiver in Liver
substanceOnly species APlasma in Plasma
substanceOnly species ARest  in Rest



#------------------------
# Initial conditions
#------------------------
AGI = 0; 
ALiver = 0; 
APlasma = 0; 		# free fraction in plasma
ARest = 0; 


#------------------------
# Concentrations
#------------------------
CGI := AGI / GI
CLiver := ALiver / Liver
CPlasma := APlasma / Plasma		# free fraction in plasma
CPlasmaTotal := CPlasma/Free		# Total PFOS concentration in plasma
CRest  := ARest  / Rest

#------------------------
# Reactions / Transfers
#------------------------

# Perfusion-limited exchanges (Plasma â†” gut & liver & rest)
APlasma   -> AGI;     QG * Free * CPlasma
AGI -> APlasma;     QG * Free * CGI/PG

APlasma   -> ALiver;     QL * Free * CPlasma
ALiver   -> APlasma;     (QL+QG) * Free * CLiver/PL

APlasma   -> ARest;    QRest * Free * CPlasma
ARest  	-> APlasma;     QRest * Free * CRest/PRest


end
