model Worley2017_PFOA_Human()

#Created: Carolina Vogs, September 2025
#Checked: Joost Westerhout, November 2025

#===========================================================
# PFOA PBK (Worley et al. 2017):
# https://www.ncbi.nlm.nih.gov/pubmed/28684146
#===========================================================

#------------------------
# Body size & base volumes
#------------------------
BW   = 70                       # kg
VLc  = 0.025                    # L/kg (liver)
VPlasmac  = 0.0428                   # L/kg (plasma)

VL   = VLc*BW                   # L
VPlasma   = VPlasmac*BW                   # L


VRest := 0.93*BW - VL - VPlasma   # Lumped Rest-of-body volume L

#------------------------
# Plasma Flows
#------------------------
QC    = 12.5*BW^0.75            # L/h (cardiac output)
QCP   = QC*(1-Htc)		# L/h cardiac output adjusted for plasma flow
QLc   = 0.25 

QL    = QLc*QCP                # L/h (liver)
QRest = QCP - QL               # L/h (rest)

Htc = 0.44
Free = 0.02

#------------------------
# Rate constants
#------------------------

# Stomach -> SI
kstoC = 3.5 				# gastric emptying time (1/(h*BW^-0.25))
ksto  = kstoC * BW^(-0.25)		# 1/h Pay attention: original code: kstoC / BW^(-0.25) # JW: The code in Worley 2017 supplemental material does state GEC/BW**(-0.25), which seems to be wrong. 
								# kabsC, kunabsC and kbileC have the same unit (1/(h*BW^-0.25)) but a different calculation; kabs = kabsc*BW**(-0.25) 
								# Chou & Lin 2019 and 2021, as well as Chen 2022 present it correctly

# SI -> liver
kabsC = 2.12  				#rate of absorption of chemical from small intestine to liver (1/(h*BW^-0.25))
kabs  = kabsC * BW^(-0.25)		# 1/h  

# Stomach -> Liver
k0C   = 1.0  				# rate of uptake from the stomach into the liver (1/(h*BW^-0.25))
k0 = k0C * BW^(-0.25)			# 1/h Pay attention: original code k0 = k0C / BW^(-0.25)  # JW: The code in Worley 2017 supplemental material does state K0C/BW**(-0.25), which seems to be wrong. 
								# kabsC, kunabsC and kbileC have the same unit (1/(h*BW^-0.25)) but a different calculation; kabs = kabsc*BW**(-0.25) 
								# Chou & Lin 2019 and 2021, as well as Chen 2022 present it correctly

kfeC = 7.06e-5 				# 1/(h*BW^-0.25)
kfe = kfeC * BW^(-0.25)			# 1/h

# SI -> Feces
kbiliaryC = 0.0001 			# 1/(h*BW^-0.25)
kbiliary = kbiliaryC * BW^(-0.25)	# 1/h

#------------------------
# Partition coefficients
#------------------------
PL = 1.03
PRest = 1  # JW: Value from Worley 2017 supplemental material is 0.11

#------------------------
# Compartments (sizes)
#------------------------
compartment Stomach; Stomach = 1
compartment SI;      SI = 1
compartment Liver;   Liver = VL
compartment Plasma;  Plasma = VPlasma
compartment Rest;    Rest  = VRest
compartment Feces;   Feces  = 1


#------------------------
# Species (amount states)
#------------------------
substanceOnly species AStomach in Stomach
substanceOnly species ASI in SI
substanceOnly species ALiver in Liver
substanceOnly species APlasma in Plasma
substanceOnly species ARest  in Rest
substanceOnly species AFeces  in Feces


#------------------------
# Initial conditions
#------------------------
AStomach = 0
ASI = 0 
ALiver = 0 
APlasma = 0 	# free fraction in plasma
ARest = 0 
AFeces = 0


#------------------------
# Concentrations 
#------------------------
CLiver := ALiver / Liver
CPlasma := APlasma / Plasma	# free concentration in plasma
CPlasmaTotal := CPlasma/Free	# Total PFOA concentration in plasma
CRest  := ARest  / Rest

#------------------------
# Reactions / Transfers
#------------------------

# Stomach -> SI & Liver
AStomach -> ASI;    ksto * AStomach
AStomach -> ALiver; k0 * AStomach

# SI -> Liver & Feces
ASI -> ALiver;      kabs * ASI
ASI -> AFeces;      kfe * ASI

# Liver ↔ Plasma & Feces
APlasma -> ALiver;     QL * Free * CPlasmaTotal 
ALiver -> APlasma;     QL * Free * CLiver/PL
ALiver -> AFeces;      kbiliary * ALiver

# Rest ↔ Plasma
APlasma   -> ARest;    QRest * Free * CPlasmaTotal
ARest  	-> APlasma;     QRest * Free * CRest/PRest


end
