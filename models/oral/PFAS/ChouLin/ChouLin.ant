model Chou_Lin2021_PFOS_Human()

#Created: Carolina Vogs, September 2025
#Checked: Joost Westerhout, November 2025

#===========================================================
# PFOS PBK model Chou and Lin et al. 2021 (revised from Worley et al. 2015, 2017):
# https://www.ncbi.nlm.nih.gov/pubmed/38642821
#===========================================================

#------------------------
# Body size & base volumes
#------------------------
BW   = 70                       # kg
VLc  = 0.026                    # L/kg (liver)
VPlasmac  = 0.0428                   # L/kg (plasma)

VL   = VLc*BW                   # L
VPlasma   = VPlasmac*BW                   # L


VRest := 0.93*BW - VL - VPlasma   # Lumped Rest-of-body volume L 

#------------------------
# Plasma Flows
#------------------------
QC    = 12.5*BW^0.75            # L/h (cardiac output) # JW: Value in Chou & Lin 2021 HMod.R is 16.4*BW^0.75. 12.5*BW^0.75 is from Chou & Lin 2019 Mrgsolvecode-Human.R
QPlasma   = QC*(1-Htc)		# L/h cardiac output adjusted for plasma flow
QLc   = 0.25

QL    = QLc*QPlasma                # L/h (liver)
QRest = QPlasma - QL               # L/h (rest)

Htc = 0.44
Free = 0.025  # JW: Value in Chou & Lin 2021 HMod.R is 0.014. 0.025 is from Chou & Lin 2019 Mrgsolvecode-Human.R

#------------------------
# Rate constants - same values as for PFOA?
#------------------------

# Stomach -> SI
kstoC = 3.51 				# gastric emptying time (1/(h*BW^-0.25))
ksto  = kstoC * BW^(-0.25)		# 1/h 

# SI -> liver
kabsC = 2.12  				#rate of absorption of chemical from small intestine to liver (1/(h*BW^-0.25))
kabs  = kabsC * BW^(-0.25)		# 1/h  

# Stomach -> Liver
k0C   = 1.0  				# rate of uptake from the stomach into the liver (1/(h*BW^-0.25))
k0 = k0C * BW^(-0.25)			# 1/h 


kfeC = 7.06e-5 				# 1/(h*BW^-0.25) # JW: Value in Chou & Lin 2021 HMod.R is 7.05e-5. 7.06e-5 is from Chou & Lin 2019 Mrgsolvecode-Human.R
kfe = kfeC * BW^(-0.25)			# 1/h

# SI -> Feces
kbiliaryC = 0.0001			# 1/(h*BW^-0.25) # JW: Value in Chou & Lin 2021 HMod.R is 1.3e-4. 0.0001 is from Chou & Lin 2019 Mrgsolvecode-Human.R
kbiliary = kbiliaryC * BW^(-0.25)	# 1/h

#------------------------
# Partition coefficients
#------------------------
PL = 2.67  # JW: Value in Chou & Lin 2021 HMod.R is 2.03. 2.67 is from Chou & Lin 2019 Mrgsolvecode-Human.R
PRest = 1  # JW: Value in Chou & Lin 2021 HMod.R and Chou & Lin 2019 Mrgsolvecode-Human.R is 0.20

#------------------------
# Compartments (sizes)
#------------------------
compartment Stomach; Stomach = 1
compartment SI;      SI = 1
compartment Liver;   Liver = VL
compartment Plasma;  Plasma = VPlasma
compartment Rest;    Rest  = VRest
compartment Feces;   Feces  = 1


#------------------------
# Species (amount states)
#------------------------
substanceOnly species AStomach in Stomach
substanceOnly species ASI in SI
substanceOnly species ALiver in Liver
substanceOnly species APlasma in Plasma
substanceOnly species ARest  in Rest
substanceOnly species AFeces  in Feces


#------------------------
# Initial conditions
#------------------------
AStomach = 0
ASI = 0 
ALiver = 0 
APlasma = 0 		# free fraction in plasma
ARest = 0 
AFeces = 0


#------------------------
# Concentrations 
#------------------------
CLiver := ALiver / Liver
CPlasma := APlasma / Plasma	# free concentration in plasma
CPlasmaTotal := CPlasma/Free	# Total PFOS concentration in plasma
CRest  := ARest  / Rest

#------------------------
# Reactions / Transfers
#------------------------

# Stomach -> SI & Liver
AStomach -> ASI;    ksto * AStomach
AStomach -> ALiver; k0 * AStomach

# SI -> Liver & Feces
ASI -> ALiver;      kabs * ASI
ASI -> AFeces;      kfe * ASI

# Liver ↔ Plasma & Feces
APlasma -> ALiver;     QL * Free * CPlasmaTotal
ALiver -> APlasma;     QL * Free * CLiver/PL
ALiver -> AFeces;      kbiliary * ALiver

# Rest ↔ Plasma
APlasma   -> ARest;    QRest * Free * CPlasmaTotal
ARest  	-> APlasma;     QRest * Free * CRest/PRest


end
