model Shin2004_BPA_Human()


#Created: Carolina Vogs, September 2025
#Checked:

#===========================================================
# BPA PBK - Shin et al. 2004
# SILume -> SITissue -> Liver <- Spleen, Stomach via portal vein
# Perfusion-limited: Blood <-> Liver, Blood <-> Rest
# Hepatic metabolism: intrinsic clearance
# https://www.ncbi.nlm.nih.gov/pubmed/15513896
#===========================================================

#------------------------
# Body size & base volumes
#------------------------
BW   = 70                          # kg
VL   = 1700/1000                   # L
VB   = 5530/1000                    # L
VSt  = 200/1000               # L
VSpleen  = 200/1000                # L
VSI = 1700/1000			   # L


# Lumped Rest-of-body volume with floor
VRest := 0.84*BW - VL - VB - VSt - VSI - VSpleen

#------------------------
# Flows 
#------------------------
QC   = 15*BW^0.75                  # L/h (cardiac output)
QL   = 1646.7/1000*60              # from ml/min to L/h (liver)
QSpleen   = 76.7/1000*60           # from ml/min to L/h (Spleen)
QSt  = 38.3/1000*60           # from ml/min to L/h (Stomach)
QSI  = 1100/1000*60          	   # from ml/min to L/h (Small intestine)

QRest= QC - QL  - QSpleen - QSt - QSI    # L/h (lumped rest)

#------------------------
# Partitions (tissue:blood)
#------------------------
RBS = 1.1				# Blood-to-serum ratio
PL   = 5.7 	                	# liver:blood 
PSpleen = 2.8				# Spleen:blood
PSt = 4.6				# Stomach:blood
PSI = 45.96				# Small Intestine: Blood
PRest= 1.0                         	# effective lumped rest:blood (can adjust if needed)

#------------------------
# GI kinetics 
#------------------------
kabs = 0.0006 * 60                       # 1/h  

#------------------------
# Hepatic intrinsic clearance 
#------------------------

CLint = 116.6   # L/h

#------------------------
# Compartments
#------------------------
compartment Stomach;  Stomach = 1.0
compartment IntestinalLumen;  IntestinalLumen = 1
compartment Spleen;   Spleen  = VSpleen
compartment SmallIntestine;   SmallIntestine  = VSI
compartment Liver;    Liver   = VL
compartment Blood;    Blood   = VB
compartment Rest;     Rest    = VRest


#------------------------
# Species (amount states)
#------------------------
substanceOnly species ASt   in Stomach
substanceOnly species AIL    in IntestinalLumen
substanceOnly species ASpleen in Spleen
substanceOnly species ASI   in SmallIntestine
substanceOnly species ALiver  in Liver
substanceOnly species ABlood  in Blood
substanceOnly species ARest   in Rest

substanceOnly species AMet  in Liver   

#------------------------
# Initial conditions
#------------------------
AIL = 0; 
ASI = 0; 
ASpleen = 0; 
ASt = 0

ALiver = 0; 
ABlood= 0; 
ARest = 0
AMet = 0;

#------------------------
# Concentrations (µmol/L)
#------------------------
CIL   := AIL / IntestinalLumen
CSI   := ASI  / SmallIntestine
CSpleen := ASpleen / Spleen
CSt := ASt / Stomach
CLiver  := ALiver / Liver
CBlood  := ABlood / Blood * RBS
CRest := ARest   / Rest


# Hepatic metabolism (intrinsic clearance on liver-venous conc)
Met := CLint * (CLiver/PL)   

#------------------------
# GI transfers & absorptions
#------------------------
AIL -> ASI;     kabs * AIL


ASI -> ALiver;  QSI * CSI/PSI
ABlood -> ASI;  QSI * CBlood


ASpleen -> ALiver;  QSpleen  * CSpleen/PSpleen
ABlood -> ASpleen;  QSpleen * CBlood

ASt -> ALiver;  QSt  * CSt/PSt
ABlood -> ASt;  QSt * CBlood

#------------------------
# Perfusion-limited exchanges
#------------------------
# Liver ↔ Blood 
ABlood -> ALiver;   (QL-QSpleen-QSt-QSI)   * CBlood
ALiver -> ABlood;   QL   * CLiver/PL

# Rest ↔ Blood
ABlood -> ARest;     QRest * CBlood
ARest   -> ABlood;   QRest * CRest/PRest

#------------------------
# Hepatic metabolism sink (parent → AMet)
#------------------------
ALiver -> AMet;   CLint * (CLiver/PL)   

end
